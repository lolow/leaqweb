***********************
* ETEM model for LEAQ *
***********************
* Version 3.0.1
* Maintainer: L. Drouet
***********************

$OFFLISTING
$ONEMPTY
$OFFDIGIT

$INCLUDE '#{@f_inc}'

set C_MAP(P,C);
set P_CONS(C,P);
set P_PROD(C,P);

LOOP(FLOW,
  LOOP(C$C_ITEMS(FLOW,C),
    LOOP(P$FLOW_IN(P,FLOW),
      C_MAP(P,C)  = yes;
      P_CONS(C,P) = yes;
    );
  );
);

LOOP(FLOW,
  LOOP(C$C_ITEMS(FLOW,C),
    LOOP(P$FLOW_OUT(P,FLOW),
      C_MAP(P,C)  = yes;
      P_PROD(C,P) = yes;
    );
  );
);

parameter nb_completed_years(T);
nb_completed_years(T)=(ord(T)-1)*period_length;

parameter annualized;
set TT/1*#{@p_period_length_d}/;
annualized = sum(TT,(1+discount_rate)**(1-(ord(TT))));

parameter salvage(T,P);
salvage(T,P) = (1-(1+discount_rate)**(-period_length*(ord(T)+life(P)-nb_periods-1)))
               /((1+discount_rate)**(period_length*(nb_periods+1-ord(T))))
               /(1-(1+discount_rate)**(-period_length*life(P)));

parameter year(T);
year(T) = first_year + (ord(T)-1) * period_length;

ALIAS(TECHNOLOGY_SET,TECHNOLOGY_SUBSET);
ALIAS(T,T1);
ALIAS(P,P1);
ALIAS(C,C1);
ALIAS(FLOW,INFLOW);
ALIAS(FLOW,OUTFLOW);

Variable
  VAR_OBJ          Objective value
;

Positive Variables
  VAR_OBJINV       Investment part of objective function
  VAR_OBJFIX       Fixed costs part of objective function
  VAR_OBJVAR       Variable costs part of objective function
  VAR_OBJSAL       Salvage value part of objective function
  VAR_ICAP(T,P)    Investment in new capacity
  VAR_IMP(T,S,C)   Import activities
  VAR_EXP(T,S,C)   Export activities
  VAR_COM(T,S,P,C) Commodity flow
;

Positive Variables
  COMMODITY_SET(T,S,AGG)
  CAPACITY(T,P)
  ACTIVITY(T,S,P)
  TOTAL_NET_AMOUNT(T,S,C)
;

Equations
  EQ_OBJ     Objective function
  EQ_OBJINV  Investment part of objective function
  EQ_OBJFIX  Fixed costs part of objective function
  EQ_OBJVAR  Variable costs part of objective function.
  EQ_OBJSAL  Salvage value part of objective function.
  EQ_COMBAL  Basic commodity balance equations
  EQ_DEMBAL  Demand balance equations
  EQ_CAPACT  Capacity utilization equation
  EQ_PTRANS  Flow transformation constraint
  EQ_SHR_LO  Product share limit constraint
  EQ_SHR_UP
  EQ_SHR_FX
  EQ_PEAK    Peak activity equations
  EQ_ACT_LO  Activity bound constraints
  EQ_ACT_UP
  EQ_ACT_FX
  EQ_IMP_LO  Importation bound constraints
  EQ_IMP_UP
  EQ_IMP_FX
  EQ_EXP_LO  Exportation bound constraints
  EQ_EXP_UP
  EQ_EXP_FX
  EQ_FLO_LO  Flow bound constraints
  EQ_FLO_UP
  EQ_FLO_FX
  EQ_ICAP_LO New capacity bound constraints
  EQ_ICAP_UP
  EQ_ICAP_FX
  EQ_CAP_LO  Capacity bound constraints
  EQ_CAP_UP
  EQ_CAP_FX
  EQ_COM_NET_UP_TS  Net amount bound constraints per time slice
  EQ_COM_NET_UP_T   Net amount bound constraints per time period
  EQ_CAPACITY
  EQ_ACTIVITY
  EQ_TOTAL_NET_AMOUNT
  EQ_COM_SET           commodity_set commodities
  EQ_COM_SET_UP_T
  EQ_DEGREE_OF_USE       Maximum share of a commodity in a commodity_set
  EQ_RATE_OF_PENETRATION Rate of penetration of a commodity in a commodity_set
  EQ_TECH_SET_SHARE_BND_FX
  EQ_TECH_SET_SHARE_BND_LO
  EQ_TECH_SET_SHARE_BND_UP
;

EQ_OBJ..
VAR_OBJ =E= VAR_OBJINV + VAR_OBJFIX + VAR_OBJVAR - VAR_OBJSAL +
0.00001 * SUM(AGG,SUM(T,SUM(S,COMMODITY_SET(T,S,AGG))));

EQ_OBJINV..
VAR_OBJINV =E= SUM((T,P), cost_icap(T,P)*VAR_ICAP(T,P)
               /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJFIX..
VAR_OBJFIX =E= SUM((T,P), annualized*cost_fom(T,P)*
               CAPACITY(T,P)
               /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJVAR..
VAR_OBJVAR =E= SUM(T, annualized*
    (
      SUM(S,
         SUM(P, cost_vom(T,P) * ACTIVITY(T,S,P) )
       + SUM(IMPORT, cost_imp(T,S,IMPORT) * VAR_IMP(T,S,IMPORT))
       - SUM(EXPORT, cost_exp(T,S,EXPORT) * VAR_EXP(T,S,EXPORT))
       + SUM(P,SUM(C$C_MAP(P,C), cost_delivery(T,S,P,C) * VAR_COM(T,S,P,C) ))
      )
    )
    /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJSAL..
VAR_OBJSAL =E= SUM(P,SUM(T$( (ord(T) ge avail(P)) and ( (ord(T)+life(P)) gt (nb_periods+1) ) ),
    salvage(T,P)*cost_icap(T,P)*VAR_ICAP(T,P)/((1+discount_rate)**(nb_completed_years(T)))
    ) );

EQ_COMBAL(T,S,C)$(not DEM(C))..
  ( SUM(P$P_PROD(C,P), VAR_COM(T,S,P,C)) + VAR_IMP(T,S,C)$IMPORT(C) ) *
    network_efficiency(C)
  =G=
    SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXPORT(C);

EQ_DEMBAL(T,S,DEM)..
   SUM(P$P_PROD(DEM,P), VAR_COM(T,S,P,DEM) )
  =G=
   frac_dem(S,DEM)*demand(T,DEM);

EQ_CAPACT(T,S,P)..
   ACTIVITY(T,S,P)
  =L=
   avail_factor(S,P)*cap_act(P)*fraction(S)*CAPACITY(T,P);

EQ_PTRANS(T,S,P,INFLOW,OUTFLOW)$( (eff_flo(INFLOW,OUTFLOW) gt 0) and FLOW_IN(P,INFLOW) and FLOW_OUT(P,OUTFLOW) )..
  SUM(C$C_ITEMS(OUTFLOW,C), VAR_COM(T,S,P,C) )
  =E=
  eff_flo(INFLOW,OUTFLOW) *
  SUM(C$C_ITEMS(INFLOW,C), VAR_COM(T,S,P,C) );

EQ_SHR_LO(T,S,P,FLOW,C)$( (flo_share_lo(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =G= flo_share_lo(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_SHR_UP(T,S,P,FLOW,C)$( (flo_share_up(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =L= flo_share_up(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_SHR_FX(T,S,P,FLOW,C)$( (flo_share_fx(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =E= flo_share_fx(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_PEAK(T,S,C)$(peak_reserve(T,S,C) ne 0)..
   1/(1+peak_reserve(T,S,C))*(
    SUM(FLOW$C_ITEMS(FLOW,C),
      SUM(P$(P_PROD(C,P) and FLOW_ACT(P,FLOW)),
        cap_act(P)*act_flo(P,C)*peak_prod(P,S,C)*fraction(S)*CAPACITY(T,P)
       ) +
      SUM(P$(P_PROD(C,P) and (not FLOW_ACT(P,FLOW))),
        peak_prod(P,S,C)*VAR_COM(T,S,P,C)
      )
    )
    + VAR_IMP(T,S,C)$IMPORT(C)
   )
   =G=
    SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C) ) +
    VAR_EXP(T,S,C)$EXPORT(C);

EQ_ACT_LO(T,S,P)$(act_bnd_lo(T,S,P) gt 0)..
ACTIVITY(T,S,P) =G= act_bnd_lo(T,S,P);

EQ_ACT_UP(T,S,P)$(act_bnd_up(T,S,P) ge 0)..
ACTIVITY(T,S,P) =L= act_bnd_up(T,S,P);

EQ_ACT_FX(T,S,P)$(act_bnd_fx(T,S,P) ge 0)..
ACTIVITY(T,S,P) =E= act_bnd_fx(T,S,P);

EQ_IMP_LO(T,S,IMPORT)$(imp_bnd_lo(T,S,IMPORT) gt 0)..
VAR_IMP(T,S,IMPORT) =G= imp_bnd_lo(T,S,IMPORT);

EQ_IMP_UP(T,S,IMPORT)$(imp_bnd_up(T,S,IMPORT) ge 0)..
VAR_IMP(T,S,IMPORT) =L= imp_bnd_up(T,S,IMPORT);

EQ_IMP_FX(T,S,IMPORT)$(imp_bnd_fx(T,S,IMPORT) ge 0)..
VAR_IMP(T,S,IMPORT) =E= imp_bnd_fx(T,S,IMPORT);

EQ_EXP_LO(T,S,EXPORT)$(exp_bnd_lo(T,S,EXPORT) gt 0)..
VAR_EXP(T,S,EXPORT) =G= exp_bnd_lo(T,S,EXPORT);

EQ_EXP_UP(T,S,EXPORT)$(exp_bnd_up(T,S,EXPORT) ge 0)..
VAR_EXP(T,S,EXPORT) =L= exp_bnd_up(T,S,EXPORT);

EQ_EXP_FX(T,S,EXPORT)$(exp_bnd_fx(T,S,EXPORT) ge 0)..
VAR_EXP(T,S,EXPORT) =E= exp_bnd_fx(T,S,EXPORT);

EQ_FLO_LO(T,S,P,FLOW)$( (flo_bnd_lo(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =G= flo_bnd_lo(T,S,FLOW);

EQ_FLO_UP(T,S,P,FLOW)$( (flo_bnd_up(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =L= flo_bnd_up(T,S,FLOW);

EQ_FLO_FX(T,S,P,FLOW)$( (flo_bnd_fx(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =E= flo_bnd_fx(T,S,FLOW);

EQ_ICAP_LO(T,P)$(icap_bnd_lo(T,P) gt 0)..
VAR_ICAP(T,P) =G= icap_bnd_lo(T,P);

EQ_ICAP_UP(T,P)$(icap_bnd_up(T,P) ge 0)..
VAR_ICAP(T,P) =L= icap_bnd_up(T,P);

EQ_ICAP_FX(T,P)$(icap_bnd_fx(T,P) ge 0)..
VAR_ICAP(T,P) =E= icap_bnd_fx(T,P);

EQ_CAP_LO(T,P)$(cap_bnd_lo(T,P) gt 0)..
CAPACITY(T,P)=G= cap_bnd_lo(T,P);

EQ_CAP_UP(T,P)$(cap_bnd_up(T,P) ge 0)..
CAPACITY(T,P)=L= cap_bnd_up(T,P);

EQ_CAP_FX(T,P)$(cap_bnd_fx(T,P) ge 0)..
CAPACITY(T,P)=E= cap_bnd_fx(T,P);

EQ_COM_NET_UP_TS(T,S,C)$(com_net_bnd_up_ts[t,s,c] ge 0)..
TOTAL_NET_AMOUNT(T,S,C)
=L= com_net_bnd_up_ts(T,S,C);

EQ_COM_NET_UP_T(T,C)$(com_net_bnd_up_t[t,c] ge 0)..
SUM(S,TOTAL_NET_AMOUNT(T,S,C))
=L= com_net_bnd_up_t(T,C);

EQ_CAPACITY(T,P)..
  CAPACITY(T,P) =E= SUM(T1$( (ord(T1) le ord(T)) and (ord(T1) ge (ord(T)-life(P)+1)) and (ord(T1) ge avail(P)) ),
                    VAR_ICAP(T1,P))+fixed_cap(T,P);

EQ_ACTIVITY(T,S,P)..
  ACTIVITY(T,S,P) =E= SUM(FLOW$FLOW_ACT(P,FLOW), SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C) / act_flo(P,C) ));

EQ_TOTAL_NET_AMOUNT(T,S,C)..
  TOTAL_NET_AMOUNT(T,S,C) =E=
  ( SUM(P$P_PROD(C,P), VAR_COM(T,S,P,C)) + VAR_IMP(T,S,C)$IMPORT(C) ) -
  ( SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXPORT(C) );

EQ_COM_SET(T,S,AGG)..
  COMMODITY_SET(T,S,AGG)
  =E= SUM(C$C_AGG(AGG,C), agg_coef(C,AGG) * ( SUM(P$P_PROD(C,P), VAR_COM(T,S,P,C)) + VAR_IMP(T,S,C)$IMPORT(C) ) ) ;

EQ_COM_SET_UP_T(T,AGG)$(commodity_set_annual_up(T,AGG) ge 0)..
  SUM(S,COMMODITY_SET(T,S,AGG)) =L= commodity_set_annual_up(T,AGG);

EQ_DEGREE_OF_USE(T,C,AGG)$(C_AGG(AGG,C) and (degree_of_use(t,c,agg) gt 0))..
   SUM(S,( SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXPORT(C) ))
  =G= degree_of_use(T,C,AGG) *
   SUM(C1$C_AGG(AGG,C1),SUM(S,( SUM(P$P_CONS(C1,P), VAR_COM(T,S,P,C1)) + VAR_EXP(T,S,C1)$EXPORT(C1) )));

EQ_RATE_OF_PENETRATION(T,C)$(ord(T) gt 1 and rate_of_penetration(t,c) ne 0)..
  SUM(S,SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXPORT(C)) =L=
  (1+rate_of_penetration(T,C)) *
  SUM(S,SUM(P$P_CONS(C,P), VAR_COM(T-1,S,P,C)) + VAR_EXP(T-1,S,C)$EXPORT(C)) ;

EQ_TECH_SET_SHARE_BND_UP(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET)$(technology_set_share_bnd_up(t,technology_subset,technology_set) gt 0)..
  SUM(P$P_TECHNOLOGY_SET(TECHNOLOGY_SUBSET,P),CAPACITY(T,P))
  =L= technology_set_share_bnd_up(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET) * SUM(P1$P_TECHNOLOGY_SET(TECHNOLOGY_SET,P1),CAPACITY(T,P1));

EQ_TECH_SET_SHARE_BND_LO(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET)$(technology_set_share_bnd_lo(t,technology_subset,technology_set) gt 0)..
  SUM(P$P_TECHNOLOGY_SET(TECHNOLOGY_SUBSET,P),CAPACITY(T,P))
  =G= technology_set_share_bnd_lo(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET) * SUM(P1$P_TECHNOLOGY_SET(TECHNOLOGY_SET,P1),CAPACITY(T,P1));

EQ_TECH_SET_SHARE_BND_FX(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET)$(technology_set_share_bnd_fx(t,technology_subset,technology_set) gt 0)..
  SUM(P$P_TECHNOLOGY_SET(TECHNOLOGY_SUBSET,P),CAPACITY(T,P))
  =E= technology_set_share_bnd_fx(T,TECHNOLOGY_SUBSET,TECHNOLOGY_SET) * SUM(P1$P_TECHNOLOGY_SET(TECHNOLOGY_SET,P1),CAPACITY(T,P1));


MODEL ETEM /
  EQ_OBJ
  EQ_OBJINV
  EQ_OBJFIX
  EQ_OBJVAR
  EQ_OBJSAL
  EQ_COMBAL
  EQ_DEMBAL
  EQ_CAPACT
  EQ_PTRANS
  EQ_SHR_LO
  EQ_SHR_UP
  EQ_SHR_FX
  EQ_PEAK
  EQ_ACT_LO
  EQ_ACT_UP
  EQ_ACT_FX
  EQ_IMP_LO
  EQ_IMP_UP
  EQ_IMP_FX
  EQ_EXP_LO
  EQ_EXP_UP
  EQ_EXP_FX
  EQ_FLO_LO
  EQ_FLO_UP
  EQ_FLO_FX
  EQ_ICAP_LO
  EQ_ICAP_UP
  EQ_ICAP_FX
  EQ_CAP_LO
  EQ_CAP_UP
  EQ_CAP_FX
  EQ_COM_NET_UP_TS
  EQ_COM_NET_UP_T
  EQ_CAPACITY
  EQ_ACTIVITY
  EQ_TOTAL_NET_AMOUNT
  EQ_COM_SET
  EQ_COM_SET_UP_T
  EQ_DEGREE_OF_USE
  EQ_RATE_OF_PENETRATION
  EQ_TECH_SET_SHARE_BND_FX
  EQ_TECH_SET_SHARE_BND_LO
  EQ_TECH_SET_SHARE_BND_UP
/;

*OPTIONS
option limrow = 0;
option limcol = 0;
option solprint = off;
ETEM.optfile = 0;

SOLVE ETEM USING LP MINIMIZING VAR_OBJ;

file out /'#{@f_csv}'/;
put out;
out.nd = 6;
out.nw = 0;
out.sw = 0;
out.lw = 0;

put 'attribute,T,S,P,C,value' / ;

put 'VAR_OBJINV,,,,,'VAR_OBJINV.l/;
put 'VAR_OBJFIX,,,,,'VAR_OBJFIX.l/;
put 'VAR_OBJVAR,,,,,'VAR_OBJVAR.l/;
put 'VAR_OBJSAL,,,,,'VAR_OBJSAL.l/;

LOOP(T,LOOP(P$CAPACITY.l(T,P),
out.nd = 0;
put 'CAPACITY,'year(T);
out.nd = 6;
put ',,'P.tl',,'CAPACITY.l(T,P)/;
))

LOOP(T,LOOP(P$VAR_ICAP.l(T,P),
out.nd = 0;
put 'VAR_ICAP,'year(T);
out.nd = 6;
put ',,'P.tl',,'VAR_ICAP.l(T,P)/;
))

LOOP(T,LOOP(S,LOOP(P$ACTIVITY.l(T,S,P),
out.nd = 0;
put 'ACTIVITY,'year(T);
out.nd = 6;
put ','S.tl','P.tl',,'ACTIVITY.l(T,S,P)/;
)))
LOOP(T,LOOP(P$(SUM(S,ACTIVITY.l(T,S,P))),
out.nd = 0;
put 'ACTIVITY,'year(T);
out.nd = 6;
put ',ANNUAL,'P.tl',,'SUM(S,ACTIVITY.l(T,S,P))/;
))

LOOP(T,LOOP(S,LOOP(IMPORT$VAR_IMP.l(T,S,IMPORT),
out.nd = 0;
put 'VAR_IMP,'year(T);
out.nd = 6;
put ','S.tl',,'IMPORT.tl','VAR_IMP.l(T,S,IMPORT)/;
)))
LOOP(T,LOOP(IMPORT$SUM(S,VAR_IMP.l(T,S,IMPORT)),
out.nd = 0;
put 'VAR_IMP,'year(T);
out.nd = 6;
put ',ANNUAL,,'IMPORT.tl','SUM(S,VAR_IMP.l(T,S,IMPORT))/;
))

LOOP(T,LOOP(S,LOOP(EXPORT$VAR_EXP.l(T,S,EXPORT),
out.nd = 0;
put 'VAR_EXP,'year(T);
out.nd = 6;
put ','S.tl',,'EXPORT.tl','VAR_EXP.l(T,S,EXPORT)/;
)))
LOOP(T,LOOP(EXPORT$SUM(S,VAR_EXP.l(T,S,EXPORT)),
out.nd = 0;
put 'VAR_EXP,'year(T);
out.nd = 6;
put ',ANNUAL,,'EXPORT.tl','SUM(S,VAR_EXP.l(T,S,EXPORT))/;
))

LOOP(T,LOOP(S,LOOP(C$EQ_COMBAL.m(T,S,C),
out.nd = 0;
put 'C_PRICE,'year(T);
out.nd = 6;
put ','S.tl',,'C.tl','EQ_COMBAL.m(T,S,C)/;
)))
LOOP(T,LOOP(C$(SUM(S,fraction(S)*EQ_COMBAL.m(T,S,C))),
out.nd = 0;
put 'C_PRICE,'year(T);
out.nd = 6;
put ',ANNUAL,,'C.tl','SUM(S,fraction(S)*EQ_COMBAL.m(T,S,C))/;
))

LOOP(T,LOOP(S,LOOP(P,LOOP(C$(C_MAP(P,C) and VAR_COM.l(T,S,P,C)),
out.nd = 0;
put 'VAR_COM,'year(T);
out.nd = 6;
put ','S.tl','P.tl','C.tl','VAR_COM.l(T,S,P,C)/;
))))
LOOP(T,LOOP(P,LOOP(C$(C_MAP(P,C) and SUM(S,VAR_COM.l(T,S,P,C))),
out.nd = 0;
put 'VAR_COM,'year(T);
out.nd = 6;
put ',ANNUAL,'P.tl','C.tl','SUM(S,VAR_COM.l(T,S,P,C))/;
)))

LOOP(T,LOOP(DEM,
out.nd = 0;
put 'DEMAND,'year(T);
out.nd = 6;
put ',ANNUAL,,'DEM.tl','demand(T,DEM)/;
))

LOOP(T,LOOP(S,LOOP(IMPORT,
out.nd = 0;
put 'COST_IMP,'year(T);
out.nd = 6;
put ','S.tl',,'IMPORT.tl','cost_imp(T,S,IMPORT)/;
)))

LOOP(T,LOOP(S,LOOP(AGG$COMMODITY_SET.l(T,S,AGG),
out.nd = 0;
put 'COMMODITY_SET,'year(T);
out.nd = 6;
put ','S.tl',,'AGG.tl','COMMODITY_SET.l(T,S,AGG)/;
)))
LOOP(T,LOOP(AGG$SUM(S,COMMODITY_SET.l(T,S,AGG)),
out.nd = 0;
put 'COMMODITY_SET,'year(T);
out.nd = 6;
put ',ANNUAL,,'AGG.tl','SUM(S,COMMODITY_SET.l(T,S,AGG))/;
))

LOOP(T,LOOP(S,LOOP(C$TOTAL_NET_AMOUNT.l(T,S,C),
out.nd = 0;
put 'TOTAL_NET_AMOUNT,'year(T);
out.nd = 6;
put ','S.tl',,'C.tl','TOTAL_NET_AMOUNT.l(T,S,C)/;
)))
LOOP(T,LOOP(C$SUM(S,TOTAL_NET_AMOUNT.l(T,S,C)),
out.nd = 0;
put 'TOTAL_NET_AMOUNT,'year(T);
out.nd = 6;
put ',ANNUAL,,'C.tl','SUM(S,TOTAL_NET_AMOUNT.l(T,S,C))/;
))

putclose out;

file status /'#{@f_status}'/;
put status;
put ETEM.modelstat;
putclose status;
