$OFFLISTING
$ONEMPTY
$OFFDIGIT
***********************
*                     *
* ETEM model for LEAQ *
*                     *
***********************
* Date     : 01/2011
* Version  : 1.0
* Authors  : L. Drouet
* Language : GAMS
* command  : gams etem.gms

$INCLUDE '#{@f_inc}'

ALIAS(T,T1);
ALIAS(C,C1);
ALIAS(FLOW,INFLOW);
ALIAS(FLOW,OUTFLOW);

Variable
  VAR_OBJ          Objective value
;

Positive Variables
  VAR_OBJINV       Investment part of objective function
  VAR_OBJFIX       Fixed costs part of objective function
  VAR_OBJVAR       Variable costs part of objective function
  VAR_OBJSAL       Salvage value part of objective function
  VAR_ICAP(T,P)    Investment in new capacity
  VAR_IMP(T,S,C)   Import activities
  VAR_EXP(T,S,C)   Export activities
  VAR_COM(T,S,P,C) Commodity flow
;

Positive Variables
  AGGREGATE(T,S,AGG)
  CAPACITY(T,P)
  ACTIVITY(T,S,P)
  TOTAL_NET_AMOUNT(T,S,C)
;

Equations
  EQ_OBJ     Objective function
  EQ_OBJINV  Investment part of objective function
  EQ_OBJFIX  Fixed costs part of objective function
  EQ_OBJVAR  Variable costs part of objective function.
  EQ_OBJSAL  Salvage value part of objective function.
  EQ_COMBAL  Basic commodity balance equations
  EQ_DEMBAL  Demand balance equations
  EQ_CAPACT  Capacity utilization equation
  EQ_PTRANS  Flow transformation constraint
  EQ_SHR_LO  Product share limit constraint
  EQ_SHR_UP
  EQ_SHR_FX
  EQ_PEAK    Peak activity equations
  EQ_ACT_LO  Activity bound constraints
  EQ_ACT_UP
  EQ_ACT_FX
  EQ_IMP_LO  Importation bound constraints
  EQ_IMP_UP
  EQ_IMP_FX
  EQ_EXP_LO  Exportation bound constraints
  EQ_EXP_UP
  EQ_EXP_FX
  EQ_FLO_LO  Flow bound constraints
  EQ_FLO_UP
  EQ_FLO_FX
  EQ_ICAP_LO New capacity bound constraints
  EQ_ICAP_UP
  EQ_ICAP_FX
  EQ_CAP_LO  Capacity bound constraints
  EQ_CAP_UP
  EQ_CAP_FX
  EQ_COM_NET_UP_TS  Net amount bound constraints per time slice
  EQ_COM_NET_UP_T   Net amount bound constraints per time period
  EQ_CAPACITY
  EQ_ACTIVITY
  EQ_TOTAL_NET_AMOUNT
  EQ_AGG           Aggregate commodities
  EQ_DEGREE_OF_USE Maximum share of a commodity in an aggregate
  EQ_RATE_OF_PENETRATION rate of penetration of a commodity in an aggregate
;

EQ_OBJ..
VAR_OBJ =E= VAR_OBJINV + VAR_OBJFIX + VAR_OBJVAR - VAR_OBJSAL;

EQ_OBJINV..
VAR_OBJINV =E= SUM((T,P), cost_icap(T,P)*VAR_ICAP(T,P)
               /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJFIX..
VAR_OBJFIX =E= SUM((T,P), annualized*cost_fom(T,P)*
               CAPACITY(T,P)
               /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJVAR..
VAR_OBJVAR =E= SUM(T, annualized*
    (
      SUM(S,
         SUM(P, cost_vom(T,P) * ACTIVITY(T,S,P) )
       + SUM(IMP, cost_imp(T,S,IMP) * VAR_IMP(T,S,IMP))
       - SUM(EXP, cost_exp(T,S,EXP) * VAR_EXP(T,S,EXP))
       + SUM(P,SUM(C$C_MAP(P,C), cost_delivery(T,S,P,C) * VAR_COM(T,S,P,C) ))
      )
    )
    /((1+discount_rate)**(nb_completed_years(T))));

EQ_OBJSAL..
VAR_OBJSAL =E= SUM(P,SUM(T$( (ord(T) ge avail(P)) and ( (ord(T)+life(P)) gt (nb_periods+1) ) ),
    salvage(T,P)*cost_icap(T,P)*VAR_ICAP(T,P)/((1+discount_rate)**(nb_completed_years(T)))
    ) );

EQ_COMBAL(T,S,C)$(not DEM(C))..
  ( SUM(P$P_PROD(C,P), VAR_COM(T,S,P,C)) + VAR_IMP(T,S,C)$IMP(C) ) *
    network_efficiency(C)
  =G=
    SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXP(C);

EQ_DEMBAL(T,S,DEM)..
   SUM(P$P_PROD(DEM,P), VAR_COM(T,S,P,DEM) )
  =G=
   frac_dem(S,DEM)*demand(T,DEM);

EQ_CAPACT(T,S,P)..
   ACTIVITY(T,S,P)
   =L=
   avail_factor(T,S,P)*cap_act(P)*fraction(S)*CAPACITY(T,P);

EQ_PTRANS(T,S,P,INFLOW,OUTFLOW)$( (eff_flo(INFLOW,OUTFLOW) gt 0) and FLOW_IN(P,INFLOW) and FLOW_OUT(P,OUTFLOW) )..
  SUM(C$C_ITEMS(OUTFLOW,C), VAR_COM(T,S,P,C) )
  =E=
  eff_flo(INFLOW,OUTFLOW) *
  SUM(C$C_ITEMS(INFLOW,C), VAR_COM(T,S,P,C) );

EQ_SHR_LO(T,S,P,FLOW,C)$( (flo_share_lo(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =G= flo_share_lo(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_SHR_UP(T,S,P,FLOW,C)$( (flo_share_up(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =L= flo_share_up(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_SHR_FX(T,S,P,FLOW,C)$( (flo_share_fx(FLOW,C) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) and C_ITEMS(FLOW,C) )..
  VAR_COM(T,S,P,C) =E= flo_share_fx(FLOW,C)*sum(C1$C_ITEMS(FLOW,C1), VAR_COM(T,S,P,C1));

EQ_PEAK(T,S,C)$(peak_reserve(T,S,C) ne 0)..
   1/(1+peak_reserve(T,S,C))*(
    SUM(FLOW$C_ITEMS(FLOW,C),
      SUM(P$(P_PROD(C,P) and FLOW_ACT(P,FLOW)),
        cap_act(P)*act_flo(P,C)*peak_prod(P,S,C)*fraction(S)*CAPACITY(T,P)
       ) +
      SUM(P$(P_PROD(C,P) and (not FLOW_ACT(P,FLOW))),
        peak_prod(P,S,C)*VAR_COM(T,S,P,C)
      )
    )
    + VAR_IMP(T,S,C)$IMP(C)
   )
   =G=
    SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C) ) +
    VAR_EXP(T,S,C)$EXP(C);

EQ_ACT_LO(T,S,P)$(act_bnd_lo(T,S,P) gt 0)..
ACTIVITY(T,S,P) =G= act_bnd_lo(T,S,P);

EQ_ACT_UP(T,S,P)$(act_bnd_up(T,S,P) ge 0)..
ACTIVITY(T,S,P) =L= act_bnd_up(T,S,P);

EQ_ACT_FX(T,S,P)$(act_bnd_fx(T,S,P) ge 0)..
ACTIVITY(T,S,P) =E= act_bnd_fx(T,S,P);

EQ_IMP_LO(T,S,IMP)$(imp_bnd_lo(T,S,IMP) gt 0)..
VAR_IMP(T,S,IMP) =G= imp_bnd_lo(T,S,IMP);

EQ_IMP_UP(T,S,IMP)$(imp_bnd_up(T,S,IMP) ge 0)..
VAR_IMP(T,S,IMP) =L= imp_bnd_up(T,S,IMP);

EQ_IMP_FX(T,S,IMP)$(imp_bnd_fx(T,S,IMP) ge 0)..
VAR_IMP(T,S,IMP) =E= imp_bnd_fx(T,S,IMP);

EQ_EXP_LO(T,S,EXP)$(exp_bnd_lo(T,S,EXP) gt 0)..
VAR_EXP(T,S,EXP) =G= exp_bnd_lo(T,S,EXP);

EQ_EXP_UP(T,S,EXP)$(exp_bnd_up(T,S,EXP) ge 0)..
VAR_EXP(T,S,EXP) =L= exp_bnd_up(T,S,EXP);

EQ_EXP_FX(T,S,EXP)$(exp_bnd_fx(T,S,EXP) ge 0)..
VAR_EXP(T,S,EXP) =E= exp_bnd_fx(T,S,EXP);

EQ_FLO_LO(T,S,P,FLOW)$( (flo_bnd_lo(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =G= flo_bnd_lo(T,S,FLOW);

EQ_FLO_UP(T,S,P,FLOW)$( (flo_bnd_up(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =L= flo_bnd_up(T,S,FLOW);

EQ_FLO_FX(T,S,P,FLOW)$( (flo_bnd_fx(T,S,FLOW) gt 0) and (FLOW_IN(P,FLOW) or FLOW_OUT(P,FLOW)) )..
SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C)) =E= flo_bnd_fx(T,S,FLOW);

EQ_ICAP_LO(T,P)$(icap_bnd_lo(T,P) gt 0)..
VAR_ICAP(T,P) =G= icap_bnd_lo(T,P);

EQ_ICAP_UP(T,P)$(icap_bnd_up(T,P) ge 0)..
VAR_ICAP(T,P) =L= icap_bnd_up(T,P);

EQ_ICAP_FX(T,P)$(icap_bnd_fx(T,P) ge 0)..
VAR_ICAP(T,P) =E= icap_bnd_fx(T,P);

EQ_CAP_LO(T,P)$(cap_bnd_lo(T,P) gt 0)..
CAPACITY(T,P)=G= cap_bnd_lo(T,P);

EQ_CAP_UP(T,P)$(cap_bnd_up(T,P) ge 0)..
CAPACITY(T,P)=L= cap_bnd_up(T,P);

EQ_CAP_FX(T,P)$(cap_bnd_fx(T,P) ge 0)..
CAPACITY(T,P)=E= cap_bnd_fx(T,P);

EQ_COM_NET_UP_TS(T,S,C)$(com_net_bnd_up_ts[t,s,c] ge 0)..
TOTAL_NET_AMOUNT(T,S,C)
=L= com_net_bnd_up_ts(T,S,C);

EQ_COM_NET_UP_T(T,C)$(com_net_bnd_up_t[t,c] ge 0)..
SUM(S,TOTAL_NET_AMOUNT(T,S,C))
=L= com_net_bnd_up_t(T,C);

EQ_CAPACITY(T,P)..
  CAPACITY(T,P) =E= SUM(T1$( (ord(T1) le ord(T)) and (ord(T1) ge (ord(T)-life(P)+1)) and (ord(T1) ge avail(P)) ),
                    VAR_ICAP(T1,P))+fixed_cap(T,P);

EQ_ACTIVITY(T,S,P)..
  ACTIVITY(T,S,P) =E= SUM(FLOW$FLOW_ACT(P,FLOW), SUM(C$C_ITEMS(FLOW,C), VAR_COM(T,S,P,C) / act_flo(P,C) ));

EQ_TOTAL_NET_AMOUNT(T,S,C)..
  TOTAL_NET_AMOUNT(T,S,C) =E=
  ( SUM(P$P_PROD(C,P), VAR_COM(T,S,P,C)) + VAR_IMP(T,S,C)$IMP(C) ) -
  ( SUM(P$P_CONS(C,P), VAR_COM(T,S,P,C)) + VAR_EXP(T,S,C)$EXP(C) );

EQ_AGG(T,S,AGG)..
  AGGREGATE(T,S,AGG)
  =E= SUM(C$C_AGG(AGG,C), agg_coef(C,AGG) * TOTAL_NET_AMOUNT(T,S,C)) ;

EQ_DEGREE_OF_USE(T,C,AGG)$(C_AGG(AGG,C) and (degree_of_use(t,c,agg) ge 0))..
  SUM(S,agg_coef(C,AGG) * TOTAL_NET_AMOUNT(T,S,C))
  =L= degree_of_use(T,C,AGG) * SUM(S,AGGREGATE(T,S,AGG));

EQ_RATE_OF_PENETRATION(T,C,AGG)$(C_AGG(AGG,C) and (rate_of_penetration(t,c,agg) ne 0) and (ord(t) gt 1))..
  SUM(S,TOTAL_NET_AMOUNT(T,S,C))
  =L= (1+rate_of_penetration(T,C,AGG)) *
  SUM(S,TOTAL_NET_AMOUNT(T-1,S,C)) ;

MODEL ETEM /
  EQ_OBJ
  EQ_OBJINV
  EQ_OBJFIX
  EQ_OBJVAR
  EQ_OBJSAL
  EQ_COMBAL
  EQ_DEMBAL
  EQ_CAPACT
  EQ_PTRANS
  EQ_SHR_LO
  EQ_SHR_UP
  EQ_SHR_FX
  EQ_PEAK
  EQ_ACT_LO
  EQ_ACT_UP
  EQ_ACT_FX
  EQ_IMP_LO
  EQ_IMP_UP
  EQ_IMP_FX
  EQ_EXP_LO
  EQ_EXP_UP
  EQ_EXP_FX
  EQ_FLO_LO
  EQ_FLO_UP
  EQ_FLO_FX
  EQ_ICAP_LO
  EQ_ICAP_UP
  EQ_ICAP_FX
  EQ_CAP_LO
  EQ_CAP_UP
  EQ_CAP_FX
  EQ_COM_NET_UP_TS
  EQ_COM_NET_UP_T
  EQ_CAPACITY
  EQ_ACTIVITY
  EQ_TOTAL_NET_AMOUNT
  EQ_AGG
  EQ_DEGREE_OF_USE
  EQ_RATE_OF_PENETRATION
/;

*OPTIONS
option limrow = 0;
option limcol = 0;
option solprint = off;

SOLVE ETEM USING LP MINIMIZING VAR_OBJ;

file out /'#{@f_out}'/;
put out;
out.nd = 6;
out.nw = 0;
out.sw = 0;
out.lw = 0;

put 'attribute,T,S,P,C,value' / ;

put 'VAR_OBJINV,,,,,'VAR_OBJINV.l/;
put 'VAR_OBJFIX,,,,,'VAR_OBJFIX.l/;
put 'VAR_OBJVAR,,,,,'VAR_OBJVAR.l/;
put 'VAR_OBJSAL,,,,,'VAR_OBJSAL.l/;

LOOP(T,LOOP(P$CAPACITY.l(T,P),
put 'CAPACITY,'T.tl',,'P.tl',,'CAPACITY.l(T,P)/;
))

LOOP(T,LOOP(P$VAR_ICAP.l(T,P),
put 'VAR_ICAP,'T.tl',,'P.tl',,'VAR_ICAP.l(T,P)/;
))

LOOP(T,LOOP(S,LOOP(P$ACTIVITY.l(T,S,P),
put 'ACTIVITY,'T.tl','S.tl','P.tl',,'ACTIVITY.l(T,S,P)/;
)))
LOOP(T,LOOP(P$(SUM(S,ACTIVITY.l(T,S,P))),
put 'ACTIVITY,'T.tl',ANNUAL,'P.tl',,'SUM(S,ACTIVITY.l(T,S,P))/;
))

LOOP(T,LOOP(S,LOOP(IMP$VAR_IMP.l(T,S,IMP),
put 'VAR_IMP,'T.tl','S.tl',,'IMP.tl','VAR_IMP.l(T,S,IMP)/;
)))
LOOP(T,LOOP(IMP$SUM(S,VAR_IMP.l(T,S,IMP)),
put 'VAR_IMP,'T.tl',ANNUAL,,'IMP.tl','SUM(S,VAR_IMP.l(T,S,IMP))/;
))

LOOP(T,LOOP(S,LOOP(EXP$VAR_EXP.l(T,S,EXP),
put 'VAR_EXP,'T.tl','S.tl',,'EXP.tl','VAR_EXP.l(T,S,EXP)/;
)))
LOOP(T,LOOP(EXP$SUM(S,VAR_EXP.l(T,S,EXP)),
put 'VAR_EXP,'T.tl',ANNUAL,,'EXP.tl','SUM(S,VAR_EXP.l(T,S,EXP))/;
))

LOOP(T,LOOP(S,LOOP(C$EQ_COMBAL.m(T,S,C),
put 'C_PRICE,'T.tl','S.tl',,'C.tl','EQ_COMBAL.m(T,S,C)/;
)))
LOOP(T,LOOP(C$SUM(S,fraction(S)*EQ_COMBAL.m(T,S,C)),
put 'C_PRICE,'T.tl',ANNUAL,,'C.tl','SUM(S,fraction(S)*EQ_COMBAL.m(T,S,C))/;
))

LOOP(T,LOOP(S,LOOP(P,LOOP(C$(C_MAP(P,C) and VAR_COM.l(T,S,P,C)),
put 'VAR_COM,'T.tl','S.tl','P.tl','C.tl','VAR_COM.l(T,S,P,C)/;
))))
LOOP(T,LOOP(P,LOOP(C$(C_MAP(P,C) and SUM(S,VAR_COM.l(T,S,P,C))),
put 'VAR_COM,'T.tl',ANNUAL,'P.tl','C.tl','SUM(S,VAR_COM.l(T,S,P,C))/;
)))

LOOP(T,LOOP(DEM,
put 'DEMAND,'T.tl',ANNUAL,,'DEM.tl','demand(T,DEM)/;
))

LOOP(T,LOOP(S,LOOP(AGG$AGGREGATE.l(T,S,AGG),
put 'AGGREGATE,'T.tl','S.tl',,'AGG.tl','AGGREGATE.l(T,S,AGG)/;
)))
LOOP(T,LOOP(AGG$SUM(S,AGGREGATE.l(T,S,AGG)),
put 'AGGREGATE,'T.tl',ANNUAL,,'AGG.tl','SUM(S,AGGREGATE.l(T,S,AGG))/;
))

LOOP(T,LOOP(S,LOOP(C$TOTAL_NET_AMOUNT.l(T,S,C),
put 'TOTAL_NET_AMOUNT,'T.tl','S.tl',,'C.tl','TOTAL_NET_AMOUNT.l(T,S,C)/;
)))
LOOP(T,LOOP(C$SUM(S,TOTAL_NET_AMOUNT.l(T,S,C)),
put 'TOTAL_NET_AMOUNT,'T.tl',ANNUAL,,'C.tl','SUM(S,TOTAL_NET_AMOUNT.l(T,S,C))/;
))

putclose out;

file status /'#{@f_status}'/;
put status;
put ETEM.modelstat;
putclose status;